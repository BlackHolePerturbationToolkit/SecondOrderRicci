import sys
from subprocess import Popen, CalledProcessError, PIPE

def git_version():
    try:
        version = Popen(['git', 'describe', '--dirty', '--always'], stdout=PIPE).communicate()[0]
    except CalledProcessError:
        version = Popen(['git', 'describe', '--always'], stdout=PIPE).communicate()[0]
    return version.decode('utf_8').rstrip('\n')

env = DefaultEnvironment()

# pretty output
if ARGUMENTS.get('VERBOSE') != '1':
  if sys.stdout.isatty():
    env['CXXCOMSTR'] = "\033[92mCompiling\033[0m $TARGET"
    env['LINKCOMSTR'] = "\033[94mLinking\033[0m $TARGET"
  else:
    env['CXXCOMSTR'] = "Compiling $TARGET"
    env['LINKCOMSTR'] = "Linking $TARGET"

# Build options
env['LIBS']     = ['gsl', 'gslcblas', 'm', 'hdf5']
env['LIBPATH']  = ['/usr/local/lib/', '/usr/lib/x86_64-linux-gnu/hdf5/serial/']
env['LINKFLAGS']= ['-fopenmp']
env['CPPPATH']  = ['/usr/local/include', '/usr/include/hdf5/serial', '../include']
env['CXXFLAGS'] = ['-O3', '-DBOOST_DISABLE_ASSERTS', '-fopenmp', '-std=c++11', '-g',
                   '-Wall', '-Wno-unused-local-typedefs', '-DH5_USE_110_API',
                   '$(-D__GIT_VERSION="\\"' + git_version() + '\\""$)']
env['CXX']      = 'g++'

# Common source files
sources = ['Coupling.cc', 'h1.cc', 'h5wrapper.cc', 'utils.cc',
           'R2_1.cc', 'R2_2.cc', 'R2_3.cc', 'R2_4.cc', 'R2_5.cc',
           'R2_6.cc', 'R2_7.cc', 'R2_8.cc', 'R2_9.cc', 'R2_10.cc']

# Main coupling code
sources_Ricci = sources + ['Ricci.cc']
executable_Ricci = '../Ricci'

Program(executable_Ricci, sources_Ricci)

# Mass perturbation coupling code
sources_M = sources + ['Ricci_M.cc', 'h1_M.cc']
executable_M = '../Ricci_M'

Program(executable_M, sources_M)

# Spin perturbation coupling code
sources_S = sources + ['Ricci_S.cc', 'h1_S.cc']
executable_S = '../Ricci_S'

Program(executable_S, sources_S)

Decider('MD5-timestamp')
